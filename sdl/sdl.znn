

embed {
#include "SDL.h"
#include <SDL_mixer.h>
}

export import "std/expected.znn";

export import "surface.znn";
export import "texture.znn";
export import "window.znn";
export import "event.znn";

import "std/io.znn";

extern struct Mix_Chunk;

export struct SDLSound {
  Mix_Chunk mutable* chunk;
};

export SDLSound!string SDLLoadSound(string path) {
  const getResult = [](Mix_Chunk mutable* s) -> SDLSound!string {
    return SDLSound(s);
  };
  const getError = [](string s) -> SDLSound!string {
    return s;
  };
  embed {
    if (auto sound = Mix_LoadWAV(path.data()))
      return invoke(&getResult, sound);
    else
      return invoke(&getError, zenon_string::reference(Mix_GetError()));
  }
}

export string? SDLPlaySound(SDLSound* sound, int channel) {
  embed {
    if (Mix_PlayChannel(channel, sound->chunk, 0) < 0)
      return zenon_string::reference(Mix_GetError());
  }
  return null;
}

export void!string sdl_init() {
  const getError = [](string s) -> expected<void, string> {
    return "SDL init error: " + s;
  };
  embed {
    if (SDL_Init(SDL_INIT_VIDEO | SDL_INIT_AUDIO | SDL_INIT_JOYSTICK) < 0)
      return invoke(&getError, zenon_string::reference(SDL_GetError()));
    if (Mix_OpenAudio(44100, MIX_DEFAULT_FORMAT, 2, 2048) < 0)
      return invoke(&getError, zenon_string::reference(Mix_GetError()));
    Mix_ReserveChannels(1);
    SDL_JoystickEventState(SDL_ENABLE);
  }
}

export string sdl_write_directory_path(string appId) {
  embed {
    auto path = SDL_GetPrefPath("comzenon", appId.data());
    if (!path)
      return zenon_string::reference("./");
    return zenon_string::reference(path);
  }
}

export string sdl_data_directory_path() {
  embed {
    auto path = SDL_GetBasePath();
    if (!path)
      return zenon_string::reference("./");
    return zenon_string::reference(path);
  }
}

export void sdl_delay(int time) {
  embed {
    SDL_Delay(time);
  }
}

export void sdl_error_box(string title, string message) {
  embed {
    SDL_ShowSimpleMessageBox(SDL_MESSAGEBOX_ERROR, title.data(), message.data(), nullptr);
  }
}

export void!string sdl_set_clipboard(string text) {
  const getError = [](string s) -> expected<void, string> {
    return s;
  };
  embed {
    if (SDL_SetClipboardText(text.data()) != 0)
      return invoke(&getError, zenon_string::reference(SDL_GetError()));
  }
}

export string!string sdl_get_clipboard() {
  const getError = [](string s) -> expected<string, string> {
    return expected<string, string>::error(s);
  };
  const getValue = [](string s) -> expected<string, string> {
    return expected<string, string>::value(s);
  };
  embed {
    if (SDL_HasClipboardText()) {
      auto c = SDL_GetClipboardText();
      if (c[0] == '\0')
        return invoke(&getError, zenon_string::reference(SDL_GetError()));
      return invoke(&getValue, zenon_string::owned(c));
    }
  }
  return expected<string, string>::value("");
}
